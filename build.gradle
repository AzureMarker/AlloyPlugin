plugins {
    id 'org.jetbrains.intellij' version '0.4.11'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'org.jetbrains.grammarkit' version '2019.2'
    id 'idea'
}

import org.jetbrains.grammarkit.tasks.*

group 'com.rit.csci746'
version '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation files("libs/org.alloytools.alloy.dist.jar")
}

sourceSets {
    main {
        // Add the generated source files into the build
        java.srcDirs('src/main/gen')
    }
}

idea {
    module {
        // Notify IntelliJ about the generated sources
        generatedSourceDirs.add(file('src/main/gen'))
    }
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2019.2.4'
    plugins = ['PsiViewer:192-SNAPSHOT']
}
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
patchPluginXml {
    changeNotes """
      <b>0.1.0</b><br>
      Initial release of the Alloy plugin
      """
}

task generateAlloyLexer(type: GenerateLexer) {
    source = "src/main/grammars/AlloyLexer.flex"
    targetDir = "src/main/gen/edu/rit/csci746/alloyplugin/lexer"
    targetClass = "AlloyLexer"
    purgeOldFiles = true
}

task generateAlloyParser(type: GenerateParser) {
    source = "src/main/grammars/AlloyParser.bnf"
    targetRoot = "src/main/gen/"
    pathToParser = "/edu/rit/csci746/alloyplugin/parser/AlloyParser.java"
    pathToPsiRoot = "/edu/rit/csci746/alloyplugin/psi"
    purgeOldFiles = true
}

compileKotlin {
    dependsOn(generateAlloyLexer, generateAlloyParser)
}
