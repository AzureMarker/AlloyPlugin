{
  parserClass="edu.rit.csci746.alloyplugin.parser.AlloyParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Alloy"
  psiImplClassSuffix="Impl"
  psiPackage="edu.rit.csci746.alloyplugin.psi"
  psiImplPackage="edu.rit.csci746.alloyplugin.psi.impl"

  elementTypeHolderClass="edu.rit.csci746.alloyplugin.psi.AlloyTypes"
  elementTypeClass="edu.rit.csci746.alloyplugin.psi.AlloyElementType"
  tokenTypeClass="edu.rit.csci746.alloyplugin.psi.AlloyTokenType"

  extends(".*[eE]xpr")=expr
}

specification ::= [module_rule] open_rule* paragraph*

private meta comma_separated_list ::= <<param>> ( COMMA <<param>> ) *

module_rule ::= MODULE name_rule  [ LBRACKET  [EXACTLY] name_rule  (COMMA [EXACTLY] num)*    RBRACKET ]

open_rule ::= [PRIVATE]  OPEN  name_rule  [ LBRACKET <<comma_separated_list ref>> RBRACKET ]  [ AS name_rule ]

paragraph ::= factDecl | assertDecl | funDecl | cmdDecl | enumDecl | sigDecl | COMMENT

factDecl ::= FACT [name_rule] block {
    implements="edu.rit.csci746.alloyplugin.psi.AlloyNamedElement"
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyNamedElementMixin"
}

assertDecl ::= ASSERT [name_rule] block {
    implements="edu.rit.csci746.alloyplugin.psi.AlloyNamedElement"
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyNamedElementMixin"
}

funDecl ::= [PRIVATE] FUN [ref DOT] name_rule LPAREN <<comma_separated_list decl>> RPAREN COLON expr block
          | [PRIVATE] FUN [ref DOT] name_rule LBRACKET <<comma_separated_list decl>> RBRACKET COLON expr block
          | [PRIVATE] FUN [ref DOT] name_rule COLON expr block
          | [PRIVATE] PRED [ref DOT] name_rule LPAREN <<comma_separated_list decl>> LPAREN block
          | [PRIVATE] PRED [ref DOT] name_rule LBRACKET <<comma_separated_list decl>> RBRACKET block
          | [PRIVATE] PRED [ref DOT] name_rule block {
    implements="edu.rit.csci746.alloyplugin.psi.AlloyNamedElement"
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyNamedDeclListMixin"
}

cmdDecl ::= [name_rule COLON] (RUN|CHECK) (name_rule|block) [scope] {
    implements="edu.rit.csci746.alloyplugin.psi.AlloyNamedElement"
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyNamedElementMixin"
}

// TODO: Is EXPECT valid? It does not appear in the Alloy grammar documentation
scope ::= FOR NUMBER [BUT <<comma_separated_list typescope>>] //[EXPECT (0|1)]
        | FOR             <<comma_separated_list typescope>>  //[EXPECT (0|1)]
//        | [EXPECT (0|1)]

typescope ::= [EXACTLY] NUMBER [name_rule|INT|SEQ]

sigDecl ::= sigQual* SIG <<comma_separated_list name_rule>> [sigExt] LBRACE <<comma_separated_list decl>>? RBRACE [block] {
    implements="edu.rit.csci746.alloyplugin.psi.AlloyNamedElement"
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyNamedDeclListMixin"
}

enumDecl ::= ENUM name_rule LBRACE <<comma_separated_list name_rule>>  RBRACE {
    implements="edu.rit.csci746.alloyplugin.psi.AlloyNamedElement"
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyNamedElementMixin"
}

sigQual ::= ABSTRACT | LONE | ONE | SOME | PRIVATE

sigExt ::= EXTENDS ref
         | IN ref (PLUS ref)*

expr ::= bin_op_expr | arrow_op_expr | compare_op_expr | implies_expr | bracket_expr | terminal_expr

// Infix expressions
bin_op_expr ::= expr binOp expr
arrow_op_expr ::= expr arrowOp expr {extends=expr}
compare_op_expr ::= expr [NOT] compareOp expr
implies_expr ::= expr IMPLIES expr ELSE expr
bracket_expr ::= expr LBRACKET <<comma_separated_list expr>> RBRACKET

// Terminal (starts with terminal) expressions
terminal_expr ::= LET <<comma_separated_list letDecl>> blockOrBar
       | quant <<comma_separated_list decl>>    blockOrBar
       | unOp expr
       | NUMBER
       | MINUS NUMBER
       | NONE
       | IDEN
       | UNIV
       | INT
       | SEQ
       // TODO: What is "seq/Int"?
//       | "seq/Int"
       | LPAREN expr RPAREN
       | [AT] name_rule
       | block
       | LBRACE <<comma_separated_list decl>> blockOrBar RBRACE {
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyTerminalExprMixin"
}

decl ::= [PRIVATE] [DISJ] <<comma_separated_list name_rule>> COLON [DISJ] expr {
    implements="edu.rit.csci746.alloyplugin.psi.AlloyNamedElement"
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyNamedElementMixin"
}

letDecl ::= name_rule EQUALS expr {
    implements="edu.rit.csci746.alloyplugin.psi.AlloyNamedElement"
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyNamedElementMixin"
}

quant ::= ALL | NO | SOME | LONE | ONE | SUM

binOp ::= OR | AND | AMPERSAND | IFF
        | IMPLIES | PLUS | MINUS | PLUSPLUS | DOMAIN | RANGE | DOT | SHL | SHA | SHR

arrowOp ::= [SOME|ONE|LONE|SET] ARROW [SOME|ONE|LONE|SET]

compareOp ::= EQUALS | IN | LT | GT | LTE | GTE

unOp ::= NOT | NO | SOME | LONE | ONE | SET | SEQ | HASH | TILDE | STAR | CARET

block ::= LBRACE expr* RBRACE

blockOrBar ::= block
             | BAR expr

name_rule ::= (THIS | ID) (SLASH ID)* {
    mixin="edu.rit.csci746.alloyplugin.psi.ext.AlloyNameRuleMixin"
}

ref ::= name_rule | UNIV | INT | SEQ // "seq/Int"
